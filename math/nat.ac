// The axioms of Peano arithmetic.
// Contains enough detail that we can prove these with "check".

type Nat: axiom

let 0: Nat = axiom

let Suc: Nat -> Nat = axiom
let 1: Nat = Suc(0)

axiom suc_injective(x: Nat, y: Nat): Suc(x) = Suc(y) -> x = y

axiom suc_neq_zero(x: Nat): Suc(x) != 0

axiom induction(f: Nat -> bool):
    f(0) & forall(k: Nat) { f(k) -> f(Suc(k)) } -> forall(n: Nat) { f(n) }

define recursion<T>(f: T -> T, a: T, n: Nat) -> T = axiom
axiom recursion_base<T>(f: T -> T, a: T): recursion(f, a, 0) = a
axiom recursion_step<T>(f: T -> T, a: T, n: Nat): recursion(f, a, Suc(n)) = f(recursion(f, a, n))

define add(a: Nat, b: Nat) -> Nat = recursion(Suc, a, b)

// Now let's have some theorems.

theorem add_zero_right(a: Nat): add(a, 0) = a

let 2: Nat = Suc(1)

theorem one_plus_one: add(1, 1) = 2

theorem add_zero_left(a: Nat): add(0, a) = a by {
    induction(add_zero_left)
}

theorem add_suc_right(a: Nat, b: Nat): add(a, Suc(b)) = Suc(add(a, b))

theorem add_suc_left(a: Nat, b: Nat): add(Suc(a), b) = Suc(add(a, b)) by {
    define f(x: Nat) -> bool = add_suc_left(a, x)
    induction(f)
    f(0)
    forall(x: Nat) {
        if f(x) {
            f(Suc(x))
        }
    }
}

theorem add_one_right(a: Nat): add(a, 1) = Suc(a)

theorem add_one_left(a: Nat): add(1, a) = Suc(a)

theorem suc_ne(a: Nat): Suc(a) != a by {
    induction(suc_ne)
    suc_ne(0)
    forall(x: Nat) {
        if suc_ne(x) {
            Suc(x) != x
            Suc(Suc(x)) != Suc(x)
            suc_ne(Suc(x))
        }
    }
}

theorem suc_suc_ne(a: Nat): Suc(Suc(a)) != a by {
    induction(suc_suc_ne)
}

theorem add_comm(a: Nat, b: Nat): add(a, b) = add(b, a) by {
    define f(x: Nat) -> bool = add_comm(x, b)
    induction(f)
    f(0)
    forall(x: Nat) {
        if f(x) {
            f(Suc(x))
        }
    }
}

theorem add_assoc(a: Nat, b: Nat, c: Nat): add(add(a, b), c) = add(a, add(b, c)) by {
    define f(x: Nat) -> bool = add_assoc(x, b, c) 
    induction(f)
    forall(x: Nat) {
        if f(x) {
            add(add(Suc(x), b), c) = add(Suc(add(x, b)), c)
            add(add(Suc(x), b), c) = Suc(add(add(x, b), c))
            Suc(add(add(x, b), c)) = Suc(add(x, add(b, c)))
            add(add(Suc(x), b), c) = Suc(add(x, add(b, c)))
            add(add(Suc(x), b), c) = add(Suc(x), add(b, c))
            f(Suc(x))
        }
    }
    f(0)
    f(a)
}

define adder(a: Nat) -> (Nat -> Nat) = function(b: Nat) { add(a, b) }

define mul(a: Nat, b: Nat) -> Nat = recursion(adder(a), 0, b)

theorem mul_zero_right(a: Nat): mul(a, 0) = 0 by {
    induction(mul_zero_right)
}

theorem mul_zero_left(a: Nat): mul(0, a) = 0 by {
    induction(mul_zero_left)
    mul_zero_left(0)
    forall(x: Nat) {
        if mul_zero_left(x) {
            mul_zero_left(Suc(x))
        }
    }
}

theorem mul_suc_right(a: Nat, b: Nat): mul(a, Suc(b)) = add(a, mul(a, b))

theorem mul_suc_left(a: Nat, b: Nat): mul(Suc(a), b) = add(b, mul(a, b)) by {
    define f(x: Nat) -> bool = mul_suc_left(a, x)
    induction(f)
    f(0)
    forall(x: Nat) {
        if f(x) {
            mul(Suc(a), x) = add(x, mul(a, x))
            mul(Suc(a), Suc(x)) = add(Suc(a), mul(Suc(a), x))
            mul(Suc(a), Suc(x)) = add(Suc(a), add(x, mul(a, x)))
            mul(a, Suc(x)) = add(a, mul(a, x))
            add(Suc(a), add(x, mul(a, x))) = add(add(Suc(a), x), mul(a, x))
            add(Suc(a), add(x, mul(a, x))) = add(Suc(add(a, x)), mul(a, x))
            add(Suc(a), add(x, mul(a, x))) = add(Suc(add(x, a)), mul(a, x))
            add(Suc(a), add(x, mul(a, x))) = add(add(Suc(x), a), mul(a, x))
            add_assoc(Suc(x), a, mul(a, x))
            add(add(Suc(x), a), mul(a, x)) = add(Suc(x), add(a, mul(a, x)))
            add(Suc(a), add(x, mul(a, x))) = add(Suc(x), add(a, mul(a, x)))
            mul(Suc(a), Suc(x)) = add(Suc(x), add(a, mul(a, x)))
            mul(Suc(a), Suc(x)) = add(Suc(x), mul(a, Suc(x)))
            f(Suc(x))
        }
    }
    f(b)
}

theorem mul_one_one: mul(1, 1) = 1 by {
    mul(1, 1) = mul(1, Suc(0))
    mul(1, 1) = add(1, mul(1, 0))
    mul(1, 1) = add(1, 0)
    mul(1, 1) = 1
}

theorem mul_comm(a: Nat, b: Nat): mul(a, b) = mul(b, a) by {
    define f(x: Nat) -> bool = mul_comm(x, b)
    induction(f)
    f(0)
    forall(x: Nat) {
        if f(x) {
            mul(x, b) = mul(b, x)
            mul(Suc(x), b) = add(b, mul(x, b))
            mul(b, Suc(x)) = add(b, mul(x, b))
            mul(Suc(x), b) = mul(b, Suc(x))
            f(Suc(x))
        }
    }
    f(a)
}

theorem add_comm_4(a: Nat, b: Nat, c: Nat, d: Nat):
    add(add(a, b), add(c, d)) = add(add(a, c), add(b, d))

theorem add_left_distrib(a: Nat, b: Nat, c: Nat):
    mul(a, add(b, c)) = add(mul(a, b), mul(a, c)) by {
    define f(x: Nat) -> bool = add_left_distrib(x, b, c)
    induction(f)
    f(0)
    forall(x: Nat) {
        if f(x) {
            mul(x, add(b, c)) = add(mul(x, b), mul(x, c))
            mul(Suc(x), add(b, c)) = add(add(b, c), mul(x, add(b, c)))
            mul(Suc(x), add(b, c)) = add(add(b, c), add(mul(x, b), mul(x, c)))
            mul(Suc(x), add(b, c)) = add(add(b, c), add(mul(x, b), mul(x, c)))
            mul(Suc(x), add(b, c)) = add(add(b, mul(x, b)), add(c, mul(x, c)))
            mul(Suc(x), add(b, c)) = add(mul(Suc(x), b), mul(Suc(x), c))
            f(Suc(x))
        }
    }
}

theorem add_right_distrib(a: Nat, b: Nat, c: Nat):
    mul(add(a, b), c) = add(mul(a, c), mul(b, c))

theorem mul_assoc(a: Nat, b: Nat, c: Nat): mul(mul(a, b), c) = mul(a, mul(b, c)) by {
    define f(x: Nat) -> bool = mul_assoc(x, b, c)
    induction(f)
    f(0)
    forall(x: Nat) {
        if f(x) {
            mul(mul(x, b), c) = mul(x, mul(b, c))
            mul(mul(Suc(x), b), c) = mul(add(b, mul(x, b)), c)
            mul(add(b, mul(x, b)), c) = add(mul(b, c), mul(mul(x, b), c))
            mul(mul(Suc(x), b), c) = add(mul(b, c), mul(mul(x, b), c))
            mul(mul(Suc(x), b), c) = add(mul(b, c), mul(x, mul(b, c)))
            mul(mul(Suc(x), b), c) = mul(Suc(x), mul(b, c))
            f(Suc(x))
        }
    }
}

define lte(a: Nat, b: Nat) -> bool = exists(c: Nat) { add(a, c) = b }

define lt(a: Nat, b: Nat) -> bool = lte(a, b) & a != b

theorem lt_not_ref(a: Nat): !lt(a, a) by {
    if lt(a, a) {
        false
    }
}

theorem lte_ref(a: Nat): lte(a, a)

theorem add_to_zero(a: Nat, b: Nat): add(a, b) = 0 -> a = 0 & b = 0 by {
    define f(x: Nat) -> bool = add_to_zero(x, b)
    induction(f)
    f(0)
    forall(x: Nat) {
        if f(x) {
            f(Suc(x))
        }
    }
}

theorem only_zero_lte_zero(a: Nat): lte(a, 0) -> a = 0

theorem not_lt_zero(a: Nat): !lt(a, 0)

theorem zero_or_suc(a: Nat): a = 0 | exists(b: Nat) { a = Suc(b) } by {
    define f(x: Nat) -> bool = (a = 0 | exists(b: Nat) { a = Suc(b) })
    induction(f)
}

theorem lte_cancel_suc(a: Nat, b: Nat): lte(Suc(a), Suc(b)) -> lte(a, b) by {
    if lte(Suc(a), Suc(b)) {
        exists(c: Nat) { add(Suc(a), c) = Suc(b) }
        add(Suc(a), c) = Suc(b)
        Suc(add(a, c)) = Suc(b)
        add(a, c) = b
        lte(a, b)
    }
}

theorem lt_cancel_suc(a: Nat, b: Nat): lt(Suc(a), Suc(b)) -> lt(a, b)

theorem lt_not_symm(a: Nat, b: Nat): lt(a, b) -> !lt(b, a) by {
    define f(x: Nat) -> bool = forall(y: Nat) { lt_not_symm(x, y) }
    induction(f)
    f(0)
    forall(x: Nat) {
        if f(x) {
            forall(y: Nat) {
                if lt(Suc(x), y) {
                    y != 0
                    zero_or_suc(y)
                    y = 0 | exists(z: Nat) { y = Suc(z) }
                    exists(z: Nat) { y = Suc(z) }
                    lt(Suc(x), Suc(z))
                    lt(x, z)
                    !lt(z, x)
                    !lt(Suc(z), Suc(x))
                    !lt(y, Suc(x))
                }
            }
            f(Suc(x))
        }
    }
}

theorem lt_diff(a: Nat, b: Nat): lt(a, b) -> exists(c: Nat) { add(a, c) = b & c != 0 } by {
    if lt(a, b) {
        lte(a, b)
        exists(c: Nat) { add(a, c) = b }
        add(a, c) = b
        c != 0
        lt_diff(a, b)
    }
}

theorem lte_trans(a: Nat, b: Nat, c: Nat): lte(a, b) & lte(b, c) -> lte(a, c) by {
    define f(z: Nat) -> bool = forall(x: Nat, y: Nat) { lte_trans(x, y, z) }
    induction(f)
    f(0)
    forall(z: Nat) {
        if f(z) {
            forall(x: Nat, y: Nat) {
                if lte(x, y) & lte(y, Suc(z)) {
                    lte(x, Suc(z))
                }
                lte_trans(x, y, Suc(z))
            }
            f(Suc(z))
        }
    }
}

theorem lt_add_suc(a: Nat, b: Nat): lt(a, add(a, Suc(b)))

theorem add_cancels_left(a: Nat, b: Nat, c: Nat): add(a, b) = add(a, c) -> b = c by {
    define f(x: Nat) -> bool = add_cancels_left(x, b, c)
    induction(f)
    f(0)
    forall(x: Nat) {
        if f(x) {
            if add(Suc(x), b) = add(Suc(x), c) {
                Suc(add(x, b)) = Suc(add(x, c))
                add(x, b) = add(x, c)
                b = c
            }
            add(Suc(x), b) = add(Suc(x), c) -> b = c
            f(Suc(x))
        }
    }
    f(a)
}

theorem add_cancels_right(a: Nat, b: Nat, c: Nat): add(b, a) = add(c, a) -> b = c by {
    if add(b, a) = add(c, a) {
        add(a, b) = add(a, c)
        b = c
    }
}

theorem add_identity_right(a: Nat, b: Nat): add(a, b) = a -> b = 0

theorem lt_suc_left(a: Nat, b: Nat): lt(a, b) -> Suc(a) = b | lt(Suc(a), b) by {
    if lt(a, b) {
        exists(c: Nat) { add(a, c) = b & c != 0 }
        exists(d: Nat) { Suc(d) = c }
        add(a, Suc(d)) = b
        if d = 0 {
            Suc(a) = b
        }
        if d != 0 {
            add(Suc(a), d) = b
            Suc(a) != b
            lte(Suc(a), b)
            lt(Suc(a), b)
        }
        Suc(a) = b | lt(Suc(a), b)
    }
}

theorem lt_suc_right(a: Nat, b: Nat): lt(a, Suc(b)) -> a = b | lt(a, b)

theorem lt_add_left(a: Nat, b: Nat, c: Nat): lt(b, c) -> lt(add(a, b), add(a, c)) by {
    if lt(b, c) {
        exists(d: Nat) { add(b, d) = c }
        d != 0
        add(add(a, b), d) = add(a, c)
        lt(add(a, b), add(a, c))
    }
}

theorem trichotomy(a: Nat, b: Nat): lt(a, b) | lt(b, a) | a = b by {
    define f(x: Nat) -> bool = trichotomy(a, x)
    induction(f)
    f(0)
    forall(x: Nat) {
        if f(x) {
            lt(a, x) | lt(x, a) | a = x
            if lt(a, x) {
                lt(a, Suc(x))
            }
            if lt(x, a) {
                Suc(x) = a | lt(Suc(x), a)
            }
            if a = x {
                lt(a, Suc(x))
            }
            f(Suc(x))
        }
    }
}

theorem division_theorem(m: Nat, n: Nat): lt(0, n) -> exists(q: Nat, r: Nat) {
    lt(r, n) & m = add(mul(q, n), r)
} by {
    if lt(0, n) {
        define f(x: Nat) -> bool = division_theorem(x, n)
        induction(f)
        f(0)
        forall(x: Nat) {
            if f(x) {
                exists(q: Nat, r: Nat) {
                    lt(r, n) & x = add(mul(q, n), r)
                }
                if Suc(r) = n {
                    Suc(x) = Suc(add(mul(q, n), r))
                    Suc(x) = add(mul(q, n), Suc(r))
                    Suc(x) = add(mul(q, n), n)
                    Suc(x) = mul(Suc(q), n)
                    Suc(x) = add(mul(Suc(q), n), 0)
                    f(Suc(x))
                }
                if Suc(r) != n {
                    lt(Suc(r), n)
                    Suc(x) = add(mul(q, n), Suc(r))
                    f(Suc(x))
                }
                f(Suc(x))
            }
        }
        division_theorem(m, n)
    }
}

define is_composite(a: Nat) -> bool = exists(b: Nat, c: Nat) {
    lt(1, b) & lt(1, c) & a = mul(b, c)
}

theorem mul_to_zero(a: Nat, b: Nat): mul(a, b) = 0 -> a = 0 | b = 0

theorem divisor_lt(a: Nat, b: Nat, c: Nat):
    a != 0 & lt(1, b) & mul(a, b) = c -> lt(a, c) by {
    if a != 0 & lt(1, b) & mul(a, b) = c {
        exists(d: Nat) { add(1, d) = b & d != 0 }
        mul(a, add(1, d)) = c
        add(1, d) = Suc(d)
        mul(a, Suc(d)) = c
        add(a, mul(a, d)) = c
        mul(a, d) != 0
        a != c
        lt(a, c)
    }
}

define is_prime(a: Nat) -> bool = lt(1, a) & !is_composite(a)

define any(f: Nat -> bool) -> bool = exists(n: Nat) { f(n) }

define divides(a: Nat, b: Nat) -> bool = exists(c: Nat) { mul(a, c) = b }

define true_below(f: Nat -> bool, n: Nat) -> bool = forall(x: Nat) { lt(x, n) -> f(x) }

theorem strong_induction(f: Nat -> bool): forall(k: Nat) {
    true_below(f, k) -> f(k)
} -> forall(n: Nat) { f(n) } by {
    if (forall(k: Nat) { true_below(f, k) -> f(k) }) {
        define g(x: Nat) -> bool = true_below(f, x)
        induction(g)
        g(0)
        forall(x: Nat) {
            if g(x) {
                true_below(f, x)
                f(x)
                forall(y: Nat) {
                    if lt(y, Suc(x)) {
                        if lt(y, x) {
                            f(y)
                        }
                        if !lt(y, x) {
                            y = x
                            f(y)
                        }
                        f(y)
                    }
                }
                true_below(f, Suc(x))
                g(Suc(x))
            }
        }
        forall(x: Nat) { g(x) }
        forall(n: Nat) {
            g(Suc(n))
            true_below(f, Suc(n))
            f(n)
        }
    }
}

theorem divides_trans(a: Nat, b: Nat, c: Nat):
    divides(a, b) & divides(b, c) -> divides(a, c) by {
    if divides(a, b) & divides(b, c) {
        exists(d: Nat) { mul(a, d) = b }
        exists(e: Nat) { mul(b, e) = c }
        mul(a, mul(d, e)) = c
        divides(a, c)
    }
}

theorem has_prime_divisor(n: Nat): lt(1, n) -> exists(p: Nat) {
    is_prime(p) & divides(p, n)
} by {
    strong_induction(has_prime_divisor)
    forall(k: Nat) {
        if true_below(has_prime_divisor, k) {
            if is_prime(k) {
                divides(k, k)
                has_prime_divisor(k)
            }
            if is_composite(k) {
                exists(b: Nat, c: Nat) {
                    lt(1, b) & lt(1, c) & k = mul(b, c)
                }
                b != 0
                lt(1, c)
                mul(b, c) = k
                lt(b, k)
                has_prime_divisor(b)
                exists(p: Nat) {
                    is_prime(p) & divides(p, b)
                }
                divides(p, k)
                has_prime_divisor(k)
            }
            has_prime_divisor(k)
        }
    }
}

struct NatPair {
    first: Nat
    second: Nat
}

// Takes (n, n!) -> (n+1, (n+1)!)
define fac_helper(p: NatPair) -> NatPair = NatPair.new(Suc(NatPair.first(p)), mul(Suc(NatPair.first(p)), NatPair.second(p)))

// Seems like a shame this isn't proven automatically.
// Every step in the proof is a pretty simple rewrite.
// Really, all the statements about factorials seem like they should be provable automatically.
theorem fac_helper_alt(a: Nat, b: Nat): fac_helper(NatPair.new(a, b)) = NatPair.new(Suc(a), mul(Suc(a), b)) by {
    let p: NatPair = NatPair.new(a, b)
    NatPair.first(p) = a
    NatPair.second(p) = b
    fac_helper(p) = NatPair.new(Suc(NatPair.first(p)), mul(Suc(NatPair.first(p)), NatPair.second(p)))
}

// Takes n -> (n, n!)
define fac_pair(n: Nat) -> NatPair = recursion(fac_helper, NatPair.new(0, 1), n)

theorem fac_pair_first(n: Nat): NatPair.first(fac_pair(n)) = n by {
    induction(fac_pair_first)
    fac_pair_first(0)
    forall(x: Nat) {
        if fac_pair_first(x) {
            exists(a: Nat) { fac_pair(x) = NatPair.new(x, a) }
            fac_pair(Suc(x)) = fac_helper(fac_pair(x))
            fac_pair(Suc(x)) = NatPair.new(Suc(x), mul(Suc(x), a))
            let b: Nat = mul(Suc(x), a)
            fac_pair(Suc(x)) = NatPair.new(Suc(x), b)
            NatPair.first(NatPair.new(Suc(x), b)) = Suc(x)
            NatPair.first(fac_pair(Suc(x))) = Suc(x)
            fac_pair_first(Suc(x))
        }
    }
}

define factorial(n: Nat) -> Nat = NatPair.second(fac_pair(n))

theorem factorial_zero: factorial(0) = 1
theorem factorial_one: factorial(1) = 1 by {
    fac_pair(0) = NatPair.new(0, 1)
    fac_pair(1) = fac_helper(fac_pair(0))
    fac_pair(1) = NatPair.new(1, mul(1, 1))
    fac_pair(1) = NatPair.new(1, 1)
}

theorem factorial_step(n: Nat): factorial(Suc(n)) = mul(Suc(n), factorial(n)) by {
    fac_pair(n) = NatPair.new(n, factorial(n))
    fac_pair(Suc(n)) = fac_helper(fac_pair(n))

    fac_pair(Suc(n)) = NatPair.new(Suc(n), mul(Suc(n), factorial(n)))
    NatPair.second(fac_pair(Suc(n))) = mul(Suc(n), factorial(n))
}

theorem divides_factorial(k: Nat, n: Nat):
    k != 0 & lte(k, n) -> divides(k, factorial(n)) by {
    if k != 0 {
        define f(x: Nat) -> bool = divides_factorial(k, x)
        induction(f)
        f(0)
        forall(x: Nat) {
            if f(x) {
                lte(k, x) -> divides(k, factorial(x))
                if lte(k, Suc(x)) {
                    if k = Suc(x) {
                        divides(k, factorial(Suc(x)))
                    }
                    if k != Suc(x) {
                        lte(k, x)
                        divides(k, factorial(x))
                        divides(factorial(x), factorial(Suc(x)))
                        divides(k, factorial(Suc(x)))
                    }
                    divides_factorial(k, Suc(x))
                }
                f(Suc(x))
            }
        }
        divides_factorial(k, n)
    }
}

theorem factorial_nondecreasing(n: Nat): lte(factorial(n), factorial(Suc(n)))

theorem lte_one_factorial(a: Nat): lte(1, factorial(a)) by {
    induction(lte_one_factorial)
    lte_one_factorial(0)
    forall(x: Nat) {
        if lte_one_factorial(x) {
            lte(1, factorial(x))
            lte(factorial(x), factorial(Suc(x)))
            lte_one_factorial(Suc(x))
        }
    }
}

theorem lt_imp_lt_suc(a: Nat, b: Nat): lt(a, b) -> lt(a, Suc(b)) by {
    if lt(a, b) {
        exists(c: Nat) { add(a, c) = b & c != 0 }
        add(a, Suc(c)) = Suc(b)
        Suc(c) != 0
        lt(a, Suc(b))
    }
}

theorem lte_mul_both(a: Nat, b: Nat, c: Nat): lte(b, c) -> lte(mul(a, b), mul(a, c)) by {
    if lte(b, c) {
        exists(d: Nat) { add(b, d) = c }
        mul(a, add(b, d)) = mul(a, c)
        add(mul(a, b), mul(a, d)) = mul(a, c)
        lte(mul(a, b), mul(a, c))
    }
}

theorem lt_mul_both(a: Nat, b: Nat, c: Nat): a != 0 & lt(b, c) -> lt(mul(a, b), mul(a, c)) by {
    if a != 0 & lt(b, c) {
        exists(d: Nat) { add(b, d) = c }
        d != 0
        mul(a, add(b, d)) = mul(a, c)
        add(mul(a, b), mul(a, d)) = mul(a, c)
        mul(a, d) != 0
        if mul(a, b) = mul(a, c) {
            add(mul(a, b), mul(a, d)) = mul(a, b)
            let y: Nat = mul(a, b)
            let z: Nat = mul(a, d)
            add(y, z) = y
            false
        }
        lte(mul(a, b), mul(a, c))
        lt(mul(a, b), mul(a, c))
    }
    a != 0 & lt(b, c) -> lt(mul(a, b), mul(a, c))
    lt_mul_both(a, b, c)
}

theorem lt_cancel_mul(a: Nat, b: Nat, c: Nat): a != 0 & lt(mul(a, b), mul(a, c)) -> lt(b, c) by {
    if a != 0 & lt(mul(a, b), mul(a, c)) {
        if lte(c, b) {
            lte(mul(a, c), mul(a, b))
            false
        }
        lt(b, c)
    }
    a != 0 & lt(mul(a, b), mul(a, c)) -> lt(b, c)
}

theorem mul_to_one(a: Nat, b: Nat): mul(a, b) = 1 -> a = 1 by {
    if mul(a, b) = 1 {
        a != 0
        !lt(a, 1)
        b != 0
        if lt(1, a) {
            lt(mul(b, 1), mul(b, a))
            lt(mul(b, 1), 1)
            mul(b, 1) = 0
            b = 0
            false
        }
        a = 1
    }
}

theorem divides_suc(a: Nat, b: Nat): divides(a, b) & divides(a, Suc(b)) -> a = 1 by {
    if divides(a, b) & divides(a, Suc(b)) {
        a != 0
        exists(c: Nat) { mul(a, c) = b }
        exists(d: Nat) { mul(a, d) = Suc(b) }
        lt(mul(a, c), mul(a, d))
        lte(c, d)
        exists(e: Nat) { add(c, e) = d }
        mul(a, add(c, e)) = Suc(b)
        add(mul(a, c), mul(a, e)) = Suc(b)
        add(b, mul(a, e)) = Suc(b)
        mul(a, e) = 1
        a = 1
    }
}

theorem exists_infinite_primes(n: Nat): exists(p: Nat) {
    lt(n, p) & is_prime(p)
} by {
    let m: Nat = Suc(factorial(n))
    lt(1, m)
    exists(p: Nat) {
        is_prime(p) & divides(p, m)
    }
    p != 1
    if lte(p, n) {
        divides_factorial(p, n)
        divides(p, factorial(n))
        false
    }
    !lte(p, n)
    lt(n, p)
}

theorem divides_zero(a: Nat): divides(a, 0)

theorem zero_divides(a: Nat): divides(0, a) -> a = 0

theorem divides_mul(a: Nat, b: Nat, d: Nat): divides(d, a) -> divides(d, mul(a, b)) by {
    if divides(d, a) {
        exists(q: Nat) { mul(q, d) = a }
        mul(mul(q, d), b) = mul(a, b)
        mul(d, mul(q, b)) = mul(a, b)
        divides(d, mul(a, b))
    }
}

theorem lte_mul(a: Nat, b: Nat): b != 0 -> lte(a, mul(a, b)) by {
    if b != 0 {
        lte(1, b)
        lte(mul(a, 1), mul(a, b))
        lte(a, mul(a, b))
    }
}

theorem divides_lte(a: Nat, b: Nat): divides(a, b) -> b = 0 | lte(a, b) by {
    if divides(a, b) & b != 0 {
        exists(q: Nat) { mul(q, a) = b }
        if q = 0 {
            b = 0
            false
        }
        lte(a, mul(q, a))
        lte(a, b)
    }
}

theorem divides_add(a: Nat, b: Nat, d: Nat):
    divides(d, a) & divides(d, b) -> divides(d, add(a, b)) by {
    if divides(d, a) & divides(d, b) {
        exists(qa: Nat) { mul(qa, d) = a }
        exists(qb: Nat) { mul(qb, d) = b }
        mul(add(qa, qb), d) = add(mul(qa, d), mul(qb, d))
        divides(d, add(a, b))
    }
}


theorem divides_symm(a: Nat, b: Nat): divides(a, b) & divides(b, a) -> a = b by {
    if divides(a, b) & divides(b, a) {
        if a = 0 {
            b = 0
            a = b
        }
        if a != 0 {
            b != 0
            lte(a, b)
            lte(b, a)
            if a != b {
                lt(a, b)
                lt(b, a)
                false
            }
            a = b
        }
    }
}